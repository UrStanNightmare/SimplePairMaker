plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.freefair.lombok" version "8.3"
}

group = 'ru.aston.oshchepkov_aa.simplepairmaker'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'ru.aston.oshchepkov_aa.simplepairmaker.PairMakerApplication'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.5.0'

    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.4.11'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.11.1'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task copyAllDependencies(type: Copy) {
    from configurations.compileClasspath
    into "${buildDir}/deps"
}
build.dependsOn(copyAllDependencies)

jar {
    manifest {
        attributes(
                'Manifest-Version': "1.0",
                'Main-Class': application.mainClass,

        )
    }
}

shadowJar {
    archiveBaseName.set('PairMaker')
    archiveClassifier.set(null)

    exclude 'META-INF/maven/**'
    exclude 'META-INF/maven/NOTICE.txt'
    exclude 'META-INF/maven/LICENSE.txt'
}

test {
    useJUnitPlatform()
}